   10 REM 3D CUBE EXAMPLE
   20 REM By Dean Belfield
   30 :
   40 MODE 1
   50 PRINT "3D Cube Demo"
   60 PRINT
   70 PRINT "Valid video modes:"
   80 PRINT "0-6, 8-18, 129, 130, 132-134, 136-143"
   90 PRINT "Modes greater than 128 are double-buffered"
  100 PRINT
  110 INPUT "SOLID (S) OR WIREFRAME (W)";F$: FILLED%=F$="S"
  120 INPUT "MODE";M%
  130 MODE M%
  140 VDU 29,640;512;:OD=128
  150 :
  160 REM Initialise the data
  170 READ CN%
  180 DIM X%(CN%),Y%(CN%),Z%(CN%),A%(CN%),B%(CN%)
  190 FOR I%=1 TO CN%: READ X%(I%),Y%(I%),Z%(I%):NEXT
  200 READ TS%
  210 DIM S%(TS%,5)
  220 FOR I%=1 TO TS%: READ S%(I%,1),S%(I%,2),S%(I%,3),S%(I%,4),S%(I%,5):NEXT
  230 REM Variables
  240 THETA=0:PSI=0:PHI=0
  250 XD=0:YD=0
  260 SD=1024
  270 REM Transform
  280 FOR I%=1 TO CN%
  290   REM Rotate
  300   XX=X%(I%):YY=Y%(I%):ZZ=Z%(I%)
  310   Y=YY*COS(PHI/180*PI)-ZZ*SIN(PHI/180*PI)
  320   ZZ=YY*SIN(PHI/180*PI)+ZZ*COS(PHI/180*PI)
  330   X=XX*COS(THETA/180*PI)-ZZ*SIN(THETA/180*PI)
  340   ZZ=XX*SIN(THETA/180*PI)+ZZ*COS(THETA/180*PI)
  350   XX=X*COS(PSI/180*PI)-Y*SIN(PSI/180*PI)
  360   YY=X*SIN(PSI/180*PI)+Y*COS(PSI/180*PI)
  370   REM Transform
  380   XX=XX+XD:YY=YY+YD
  390   REM Perspective
  400   A%(I%)=XX*SD/(OD-ZZ)
  410   B%(I%)=YY*SD/(OD-ZZ)
  420 NEXT
  430 REM Draw
  440 CLS
  450 I%=RND(-55)
  460 FOR I%=1 TO TS%
  470   GCOL 0,S%(I%,5)
  480   C1%=S%(I%,1):C2%=S%(I%,2):C3%=S%(I%,3):C4%=S%(I%,4)
  490   X1=A%(C1%):X2=A%(C2%):X3=A%(C3%):X4=A%(C4%)
  500   Y1=B%(C1%):Y2=B%(C2%):Y3=B%(C3%):Y4=B%(C4%)
  510   IF X1*(Y2-Y3)+X2*(Y3-Y1)+X3*(Y1-Y2) > 0 THEN GOTO 570
  520   IF FILLED%=0 THEN GOTO 560
  530   MOVE X1,Y1:MOVE X2,Y2:PLOT 85,X3,Y3
  540   MOVE X3,Y3:MOVE X4,Y4:PLOT 85,X1,Y1
  550   GOTO 570
  560   MOVE X1,Y1:PLOT 5,X2,Y2:PLOT 5,X3,Y3:PLOT 5,X4,Y4:PLOT 5,X1,Y1
  570 NEXT
  580 PHI=PHI+4
  590 THETA=THETA-1
  600 VDU 23,0,&C3
  610 GOTO 280
  620 REM DATA
  630 DATA 8
  640 DATA -20,20,20
  650 DATA 20,20,20
  660 DATA -20,-20,20
  670 DATA 20,-20,20
  680 DATA -20,20,-20
  690 DATA 20,20,-20
  700 DATA -20,-20,-20
  710 DATA 20,-20,-20
  720 DATA 6
  730 DATA 1,2,4,3,9
  740 DATA 7,8,6,5,10
  750 DATA 2,6,8,4,11
  760 DATA 3,7,5,1,12
  770 DATA 3,4,8,7,13
  780 DATA 1,5,6,2,14
